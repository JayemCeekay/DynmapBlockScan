
buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
	    maven { url "https://repo.mikeprimm.com" }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'eclipse'

// define the properties file
ext.configFile = file "build.properties"

configFile.withReader {
    def prop = new Properties()
    prop.load(it)
    project.ext.config = new ConfigSlurper().parse prop
}

version = project.config.Dynmap_version + project.config.Dynmap_extversion
group= "us.dynmap.forge" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "DynmapBlockScan"
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = JavaLanguageVersion.of(16) // Need this here so eclipse task generates correctly.
ext.buildNumber = System.getenv().BUILD_NUMBER ?: "Dev"

dependencies {
	implementation "us.dynmap:DynmapCoreAPI:${config.Dynmap_version}${config.Dynmap_extversion}:all"
    minecraft 'net.minecraftforge:forge:1.17.1-37.0.12'
}

minecraft {
    mappings channel: 'official', version: '1.17.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        server {
            workingDirectory project.file('run').canonicalPath
        }
    }
}

repositories {
    mavenLocal()
    maven { url "https://repo.mikeprimm.com" }
}

configurations {
    dynmap
    compile.extendsFrom dynmap
}

processResources
{
    filesMatching('META-INF/mods.toml') {
        // replace version and mcversion
        expand(
        	version: project.version + '-' + project.ext.buildNumber,
        	mcversion: "1.17.1"
    	)
    }
}

jar {
   classifier = "forge-${config.minecraft_version}"
   destinationDir = project.projectDir
   from { configurations.dynmap.collect { it.isDirectory() ? it : zipTree(it) } }
}
